plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.0.1'
    id 'java'
    id 'java-library'
}

group = 'me.noahvdaa.skinblacklister'
version = '2.0.0-SNAPSHOT'

dependencies {
    implementation project(path: ':SkinBlacklister-bukkit', configuration: 'shadow')
    implementation project(path: ':SkinBlacklister-bungeecord', configuration: 'shadow')
    implementation project(path: ':SkinBlacklister-velocity', configuration: 'shadow')
}

repositories {
    mavenCentral()
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    group = parent.group
    version = parent.version

    def targetJavaVersion = property('javaVersion') as int
    java {
        def javaVersion = JavaVersion.toVersion(targetJavaVersion)
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        if (JavaVersion.current() < javaVersion) {
            toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
            options.release = targetJavaVersion
        }
    }

    repositories {
        mavenCentral()

        maven {
            name = 'sonatype-snapshots'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }

    dependencies {
        implementation 'org.spongepowered:configurate-hocon:4.0.0'
        implementation 'net.kyori:adventure-text-minimessage:4.1.0-SNAPSHOT'
    }
}

shadowJar {
    relocate 'org.json', 'me.noahvdaa.skinblacklister.libs.json'
    relocate 'org.spongepowered.configurate', 'me.noahvdaa.skinblacklister.libs.configurate'
    relocate 'com.typesafe.config', 'me.noahvdaa.skinblacklister.libs.configurate.com.typesafe.config'
    relocate 'io.leangen.geantyref', 'me.noahvdaa.skinblacklister.libs.io.leangen.geantyref'
    relocate 'net.kyori', 'me.noahvdaa.skinblacklister.libs.kyori'
}

tasks.build.dependsOn tasks.shadowJar
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
